/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config {
    info {
        name          = 'Modular TilesFX example'
        description   = 'Modular TilesFX example'
        vendor        = 'Kordamp'
        inceptionYear = '2020'
        tags          = ['kordamp', 'javafx', 'tilesfx', 'modules']

        links {
            website      = "https://github.com/aalmiray/${rootProject.name}"
            issueTracker = "https://github.com/aalmiray/${rootProject.name}/issues"
            scm          = "https://github.com/aalmiray/${rootProject.name}.git"
        }

        specification  { enabled = false }

        implementation { enabled = false }

        people {
            person {
                id    = 'aalmiray'
                name  = 'Andres Almiray'
                url   = 'http://andresalmiray.com/'
                roles = ['developer']
                properties = [
                    twitter: 'aalmiray',
                    github : 'aalmiray'
                ]
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url  = "${rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "${rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    docs {
        javadoc {
            enabled = false
        }
    }

    publishing {
        signing             = false
        releasesRepository  = 'localRelease'
        snapshotsRepository = 'localSnapshot'
    }
}

projects {
    allprojects {
        normalization {
            runtimeClasspath {
                ignore('/META-INF/MANIFEST.MF')
            }
        }
        
        repositories {
            jcenter()
            mavenLocal()
        }
    }

    subprojects {
        path('*') {
            config {
                info {
                    description = project.name
                }

                docs {
                    javadoc {
                        enabled = false
                    }
                }
            }

            javafx {
                version = javafxVersion
                modules = [
                    'javafx.base',
                    'javafx.graphics',
                    'javafx.controls',
                    'javafx.web'
                ]
            }

            publishing {
                publications {
                    main(MavenPublication) {
                        pom.withXml {
                            asNode().dependencies.'*'.findAll {
                                it.groupId.text() == 'org.openjfx'
                            }.each {
                                it.remove(it.classifier)
                            }
                        }
                    }
                }
            }
        }
        dir('tiles') {
            dependencies {
                api project(':modular-tiles-model')
            }

            distributions {
                main {
                    contents {
                        into("/${project.name}-${project.version}")
                        from jar.outputs
                    }
                }
            }
            distTar {
                compression = Compression.GZIP
                archiveExtension = 'tar.gz'
            }
        }
    }
}

ext.tilePlugins = [
    ':tile-clock-plugin',
    ':tile-gauge-plugin',
    ':tile-sparkline-plugin'
]

task cleanPluginsPrepared(type: Delete) {
    delete project.layout.projectDirectory.dir('plugins-prepared')
}
clean.dependsOn(cleanPluginsPrepared)

task copyPluginsPrepared(type: Copy) {
    dependsOn tilePlugins.collect { plugin -> project(plugin).distTar }
    from tilePlugins.collect { plugin -> tarTree(project(plugin).distTar.archiveFile) }
    into(project.layout.projectDirectory.dir('plugins-prepared'))
}
assemble.dependsOn(copyPluginsPrepared)

/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2021 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config {
    info {
        name          = 'Modular TilesFX example'
        description   = 'Modular TilesFX example'
        vendor        = 'Kordamp'
        inceptionYear = '2020'
        tags          = ['kordamp', 'javafx', 'tilesfx', 'modules']

        links {
            website      = 'https://github.com/moditect/layrry-examples'
            issueTracker = 'https://github.com/moditect/layrry-examples/issues'
            scm          = 'https://github.com/moditect/layrry-examples.git'
        }

        specification  { enabled = false }

        implementation { enabled = false }

        people {
            person {
                id    = 'gunnarmorling'
                name  = 'Gunnar Morling'
                url   = 'http://morling.dev/'
                roles = ['developer']
                properties = [
                    twitter: 'gunnarmorling',
                    github : 'gunnarmorling'
                ]
            }
            person {
                id    = 'aalmiray'
                name  = 'Andres Almiray'
                url   = 'http://andresalmiray.com/'
                roles = ['developer', 'author']
                properties = [
                    twitter: 'aalmiray',
                    github : 'aalmiray'
                ]
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url  = "${rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "${rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    docs {
        javadoc {
            enabled = false
        }
    }

    publishing {
        signing             = false
        releasesRepository  = 'localRelease'
        snapshotsRepository = 'localSnapshot'
    }
}

projects {
    allprojects {
        normalization {
            runtimeClasspath {
                ignore('/META-INF/MANIFEST.MF')
            }
        }
        
        repositories {
            mavenCentral()
            mavenLocal()
        }
    }

    subprojects {
        path('*') {
            config {
                info {
                    description = project.name
                }

                docs {
                    javadoc {
                        enabled = false
                    }
                }
            }

            javafx {
                version = javafxVersion
                modules = [
                    'javafx.base',
                    'javafx.graphics',
                    'javafx.controls',
                    'javafx.web'
                ]
            }

            publishing {
                publications {
                    main(MavenPublication) {
                        pom.withXml {
                            asNode().dependencies.'*'.findAll {
                                it.groupId.text() == 'org.openjfx'
                            }.each {
                                it.remove(it.classifier)
                            }
                        }
                    }
                }
            }
        }
        dir('tiles') {
            dependencies {
                api project(':modular-tiles-model')
            }

            distributions {
                main {
                    contents {
                        into("/${project.name}-${project.version}")
                        from jar.outputs
                    }
                }
            }
            distTar {
                compression = Compression.GZIP
                archiveExtension = 'tar.gz'
            }
        }
    }
}

tasks.register('processResources', org.gradle.language.jvm.tasks.ProcessResources) {
    from project.layout.projectDirectory.dir('src/layers').asFile
    destinationDir = project.layout.projectDirectory.dir('staging').asFile
    expand(
        'project_version': version,
        'javafx_version': javafxVersion,
        'tilesfx_version': tilesfxVersion,
        'layrry_version': layrryVersion
    )
}
assemble.dependsOn(processResources)

ext.tilePlugins = [
    ':tile-clock-plugin',
    ':tile-gauge-plugin',
    ':tile-sparkline-plugin'
]

task cleanStaging(type: Delete) {
    delete project.layout.projectDirectory.dir('staging')
}
clean.dependsOn(cleanStaging)

task flatRepository(type: Copy) {
    from project(':modular-tiles-app').tasks.jar.archiveFile.asFile
    into project.layout.projectDirectory.dir('staging/repositories/flat')
    from project(':modular-tiles-app').configurations.runtimeClasspath
    into(project.layout.projectDirectory.dir('staging/repositories/flat'))
}

task copyJars(type: Copy) {
    dependsOn tilePlugins.collect { plugin -> project(plugin).jar }
    from tilePlugins.collect { plugin -> project(plugin).jar.archiveFile }
    into project.layout.projectDirectory.dir('staging/plugins-prepared')
    doLast {
        project.layout.projectDirectory.dir('staging/plugins').asFile.mkdirs()
    }
}
task copyZips(type: Copy) {
    dependsOn tilePlugins.collect { plugin -> project(plugin).distZip }
    from tilePlugins.collect { plugin -> project(plugin).distZip.archiveFile }
    into project.layout.projectDirectory.dir('staging/plugins-prepared')
    doLast {
        project.layout.projectDirectory.dir('staging/plugins').asFile.mkdirs()
    }
}
task copyTars(type: Copy) {
    dependsOn tilePlugins.collect { plugin -> project(plugin).distTar }
    from tilePlugins.collect { plugin -> project(plugin).distTar.archiveFile }
    into project.layout.projectDirectory.dir('staging/plugins-prepared')
    doLast {
        project.layout.projectDirectory.dir('staging/plugins').asFile.mkdirs()
    }
}
assemble.dependsOn(flatRepository, copyJars, copyZips, copyTars)
